# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Thach Nguyen (github.com/thachnn)

_realname=libatomic_ops
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7.8.0
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.hboehm.info/gc"
license=('spdx:GPL-2.0' 'spdx:MIT')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "autotools")
source=("https://github.com/ivmai/${_realname}/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        001-fix-pkgconfig.patch)
sha256sums=('15676e7674e11bda5a7e50a73f4d9e7d60452271b8acf6fd39a71fefdf89fa31'
            'dd2a7d26382f772d599c39aded85fb2ade304d1a574801e003c5f823497dfb24')

prepare() {
  cd "${srcdir}"/${_realname}-${pkgver}
  patch -Nlp1 -i "${srcdir}"/001-fix-pkgconfig.patch
}

build() {
  mkdir -p ${srcdir}/build-${MSYSTEM} && cd ${srcdir}/build-${MSYSTEM}

  declare -a _extra_config
  if check_option "debug" "y"; then
    _extra_config+=(CFLAGS="$CFLAGS -g")
  fi

  if [[ ${MSYSTEM} == MINGW64 || ${MSYSTEM} == UCRT64 ]]; then
    _extra_config+=("--disable-atomic-intrinsics")
  fi

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  ../"${_realname}-${pkgver}"/configure \
    --prefix="${MINGW_PREFIX}" \
    --{build,host,target}=${MINGW_CHOST} \
    --enable-shared \
    "${_extra_config[@]}"

  make --directory src
}

check() {
  cd ${srcdir}/build-${MSYSTEM}
  make check || echo "Tests failed"
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  # License files
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p "licenses/${_realname}"
  mv "doc/${_realname}"/{COPYING,LICENSE} "licenses/${_realname}/"
}
