# Maintainer: Thach Nguyen (github.com/thachnn)

_realname=nginx
pkgbase="mingw-w64-$_realname"
pkgname=("$MINGW_PACKAGE_PREFIX-$_realname")
pkgver=1.22.1
_tag="release-$pkgver"
pkgrel=1
pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server (mingw-w64)'
arch=(any)
mingw_arch=(mingw32 mingw64 ucrt64 clang64 clang32 clangarm64)
url='https://nginx.org'
license=('spdx:BSD-2-Clause')

depends=("$MINGW_PACKAGE_PREFIX-pcre2"
         "$MINGW_PACKAGE_PREFIX-zlib")
depends_x86_64=('libssl-1_1-x64.dll')
depends_i686=('libssl-1_1.dll')
depends_aarch64=('libssl-1_1.dll')
makedepends=("$MINGW_PACKAGE_PREFIX-gcc")

backup=('etc/nginx/fastcgi.conf'
        'etc/nginx/fastcgi_params'
        'etc/nginx/http.d/default-server.conf'
        'etc/nginx/http.d/https-server.conf'
        'etc/nginx/koi-win'
        'etc/nginx/koi-utf'
        'etc/nginx/nginx.conf'
        'etc/nginx/scgi_params'
        'etc/nginx/uwsgi_params'
        'etc/nginx/win-utf')

source=("https://github.com/nginx/$_realname/archive/$_tag.tar.gz"
        'https://github.com/msys2/MINGW-packages/raw/master/mingw-w64-pathtools/pathtools.c'
        'https://github.com/msys2/MINGW-packages/raw/master/mingw-w64-pathtools/pathtools.h'
        '001-fix-build-flags.patch'
        '002-fix-res-icon.patch'
        '003-separate-conf-file.patch'
        '004-relocation.patch'
        '005-fallback-log-stderr.patch'
        '006-support-utf8-filename.patch')
sha256sums=('d5031d02c28dc53838b8ceebf6a198eb1332c31809b5d7672eb43c365e7cf0b7'
            '08209cbf1633fa92eae7e5d28f95f8df9d6184cc20fa878c99aec4709bb257fd'
            '965d3921ec4fdeec94a2718bc2c85ce5e1a00ea0e499330a554074a7ae15dfc6'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

# Helper macros to help make tasks easier
apply_patch_with_msg() {
  for _patch in "$@"; do
    msg2 "Applying $_patch"
    patch -Nlp1 -i "$srcdir/$_patch"
  done
}

prepare() {
  cd "$srcdir/$_realname-$_tag"

  apply_patch_with_msg \
    '001-fix-build-flags.patch' \
    '002-fix-res-icon.patch' \
    '003-separate-conf-file.patch' \
    '004-relocation.patch' \
    '005-fallback-log-stderr.patch' \
    '006-support-utf8-filename.patch'

  cp -fHv "$srcdir"/pathtools.* src/os/win32/
}

build() {
  cd "$srcdir/$_realname-$_tag"

  MSYS2_ARG_CONV_EXCL='--prefix=;--sbin-path=' \
  NGX_MACHINE="$CARCH" \
  auto/configure \
    --prefix="$MINGW_PREFIX/var/lib/nginx" \
    --sbin-path="$MINGW_PREFIX/bin/nginx.exe" \
    --http-client-body-temp-path=temp/client_body \
    --http-proxy-temp-path=temp/proxy \
    --http-fastcgi-temp-path=temp/fastcgi \
    --http-scgi-temp-path=temp/scgi \
    --http-uwsgi-temp-path=temp/uwsgi \
    \
    --crossbuild=win32 \
    --with-cc=gcc \
    --with-cc-opt="$CFLAGS -DFD_SETSIZE=1024 -s -fno-strict-aliasing" \
    --with-ld-opt="$LDFLAGS -Wl,--gc-sections,--build-id=none -Wl,--exclude-libs=ALL" \
    --with-{pcre,zlib,openssl}=NONE \
    --with-http_{v2,realip,addition,sub,dav,stub_status,flv,mp4,gunzip,gzip_static}_module \
    --with-http_{auth_request,random_index,secure_link,slice}_module \
    --with-{mail,stream} \
    --with-{http,mail,stream}_ssl_module \

  make
}

package() {
  cd "$srcdir/$_realname-$_tag"
  make DESTDIR="$pkgdir" install

  #TODO install -Dm644 "../$_realname-$_ver/LICENSE.txt" "$pkgdir$MINGW_PREFIX/share/licenses/$_realname/LICENSE"
}
